/**
 * The SmartPioneerBaseServer makes P2OS-based robot platforms available. 
 * It handles all the communication with the hardware. 
 * It offers several services for controlling the robot, 
 * such as sending navigation commands to the base and providing access to the robot's odometry. 
 * Position updates can be sent to the component to overcome odometry failures.
 *
 * GPL-License: includes Code from the Player Project.
 */
ComponentDefinition SmartPioneerBaseServer {

	CoordinationSlavePort CoordinationPort implements CommNavigationObjects.BaseCoordinationService {
		with baseState realizedBy BaseStateQueryServer 
		with batteryEvent realizedBy BatteryEventServer
//		with bumperEvent realizedBy 
	}

	
	// InputPorts
	InputPort NavVelIn implements CommNavigationObjects.NavigationVelocityService { }
	InputPort LocalizationUpdate implements CommNavigationObjects.LocalizationUpdateService { }

	// Tasks
	Activity PoseUpdateTask 
	{
		Observe RobotTask;
		ActivationConstraints {
			configurable=true minActFreq = 10.0 Hz maxActFreq = 40.0 Hz
		}
		DefaultTrigger PeriodicTimer 10.0 Hz;
	}
	Activity RobotTask
	{
		InputPortLink NavVelIn { oversamplingOk undersamplingOk }
		InputPortLink LocalizationUpdate { optional oversamplingOk undersamplingOk }
		ActivationConstraints {
			configurable=false minActFreq = 10.0 Hz maxActFreq = 40.0 Hz
		}
	}
	
	// OutputPorts
	OutputPort BasePositionOut implements CommBasicObjects.BaseStateService realizedBy PoseUpdateTask { }
	OutputPort BatteryEventServer implements CommNavigationObjects.BatteryEventService realizedBy PoseUpdateTask { } 
	
	AnswerPort BaseStateQueryServer implements CommBasicObjects.BaseStateQueryService { }
	RequestHandler BaseStateQueryHandler triggeredFrom BaseStateQueryServer { Observe RobotTask; }
	
	ComponentParameters SmartPioneerBaseServerParams slave CoordinationPort;
}
