/**
 * The SmartMapperGridMap provides mapping services based on occupancy grid maps. 
 * Laser scans are taken for building a current and a longterm map.

 * The current map represents the latest environment of the robot. 
 * It can be preoccupied with grids of the longterm map and can be used for path planning e.g. with SmartPlannerBreadthFirstSearch. 
 * It contains either occupied cells or free cells (binary). An optional obstacle growing can be applied to the current map.

 * The longterm map holds cell values from 0 to 255. Values from 0 to 127 denote the traversability where 0 is completely free. 
 * Values from 128 to 255 are special values: Obstacles are marked with 128, 
 * cells occupied by obstacle growing with 129 and undeletable grids are marked with 130. 
 * The cell values can be accumulated over time to represent the environment over a longer period.

 * Both grid maps can be saved to XPM and XML and loaded from XML files.
 */
ComponentDefinition SmartMapperGridMap {
	CoordinationSlavePort CoordinationPort implements CommNavigationObjects.MapperCoordinationService {
		PrivateOperationMode currMap;
		PrivateOperationMode ltmMap; 
		PublicOperationMode CommNavigationObjects.MapperStates.BuildCurrMap {
			activates (currMap)
		}
		PublicOperationMode CommNavigationObjects.MapperStates.BuildLtmMap {
			activates (ltmMap)
		}
		PublicOperationMode CommNavigationObjects.MapperStates.BuildBothMaps {
			activates (currMap, ltmMap)
		}
	}

	InputPort LaserServiceIn implements CommBasicObjects.LaserService { }
	AnswerPort CurrQueryServer implements CommNavigationObjects.CurrGridMapQueryService { }
	AnswerPort LtmQueryServer implements CommNavigationObjects.LongTermGridMapQueryService { }
	
	Activity CurMapTask {
		InputPortLink LaserServiceIn { 
			oversamplingOk undersamplingOk
		}
		ActivationConstraints {
			configurable=true minActFreq = 10.0 Hz maxActFreq = 20.0 Hz
		}
		OperationModeBinding CoordinationPort.currMap;
	}
	Activity LtmMapTask {
		InputPortLink LaserServiceIn {
			oversamplingOk undersamplingOk
		}
		ActivationConstraints {
			configurable=true minActFreq = 2.0 Hz maxActFreq = 10.0 Hz
		}
		DefaultTrigger PeriodicTimer 2.0 Hz;
		OperationModeBinding CoordinationPort.ltmMap;
	}
	
	OutputPort CurrMapOut implements CommNavigationObjects.CurrGridMapPushService realizedBy CurMapTask { } 
	RequestHandler CurrQueryServerHandler triggeredFrom CurrQueryServer { Observe CurMapTask } 
	RequestHandler LtmQueryServerHandler triggeredFrom LtmQueryServer { Observe LtmMapTask }
	
	ComponentParameters SmartMapperGridMapParams slave CoordinationPort;
}
